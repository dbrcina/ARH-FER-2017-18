CT_CR		EQU 0FFFF0000
CT_LR		EQU 0FFFF0004
CT_STAT		EQU 0FFFF0008
CT_END		EQU 0FFFF000C

DMA_SRC		EQU 0FFFF1000
DMA_DEST	EQU 0FFFF1004
DMA_CNT		EQU 0FFFF1008
DMA_CR		EQU 0FFFF100C
DMA_START	EQU	0FFFF1010
DMA_ACK		EQU 0FFFF1014

BVJ			EQU 0FFFFFFFC

		ORG 0
		MOVE 10000,SP
		JR GLAVNI
		
		ORG 0C				;prekidni NMI potprogram
P_NMI	PUSH R0
		PUSH R1
		MOVE SR,R0
		PUSH R0
		STORE R0,(DMA_ACK)
		LOAD R0,(ODREDISTE)		
		MOVE -1,R1
		STORE R1,(R0)		;postavi -1 na kraju svakog bloka
		ADD R0,4,R0
		STORE R0,(ODREDISTE)
		POP R0
		MOVE R0,SR
		POP R1
		POP R0
		RETN
		
GLAVNI	MOVE %D1000,R0		;inicijalizacija CT-a
		STORE R0,(CT_LR)
		MOVE %B001,R0
		STORE R0,(CT_CR)
		
PETLJA	LOAD R0,(BLOKOVI)	;je li preneseno 5 blokova
		CMP R0,5
		JR_EQ KRAJ
		ADD R0,1,R0
		STORE R0,(BLOKOVI)
LOOP	LOAD R0,(CT_STAT)	;cekanje spremnosti CT-a
		CMP R0,0
		JR_EQ LOOP
		STORE R0,(CT_STAT)	;brisanje spremnosti CT-a
		
IN_DMA	MOVE BVJ,R0			;inicijalizacija DMA
		STORE R0,(DMA_SRC)
		LOAD R0,(ODREDISTE)
		STORE R0,(DMA_DEST)
		ADD R0,%D36,R0
		STORE R0,(ODREDISTE)
		MOVE 9,R0
		STORE R0,(DMA_CNT)
		MOVE %B0111,R0
		STORE R0,(DMA_CR)
		STORE R0,(CT_END)
		STORE R0,(DMA_START)
		JR PETLJA
		
KRAJ	MOVE 0,R0
		STORE R0,(CT_CR)
		STORE R0,(DMA_CR)
		HALT
		
BLOKOVI	DW 0
ODREDISTE DW 1000 
